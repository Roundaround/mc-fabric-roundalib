plugins {
  id "fabric-loom" version "1.0-SNAPSHOT"
  id "maven-publish"
  id "com.github.johnrengelman.shadow" version "7.1.2"
}

def targetJavaVersion = 17
def fullVersion = project.mod_version + "+" + project.minecraft_version

configurations.configureEach {
  version = project.mod_version
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  shadow "com.electronwill.night-config:core:3.6.5"
  shadow "com.electronwill.night-config:toml:3.6.5"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

java {
  sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
  targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

  withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release.set(targetJavaVersion)
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.property("archive_base_name")}"}
  }
}

shadowJar {
  configurations = [project.configurations.shadow]

  archiveBaseName.set(project.property("archive_base_name").toString())
  archiveVersion.set(fullVersion)
  archiveClassifier.set("shaded")

  relocate("com.electronwill.nightconfig", "me.roundaround.roundalib.shadow.nightconfig")
}

remapJar {
  dependsOn(shadowJar)
  inputFile.set(tasks.shadowJar.archiveFile)

  archiveBaseName.set(project.property("archive_base_name").toString())
  archiveVersion.set(fullVersion)
  archiveClassifier.set("")

  addNestedDependencies.set(true)
}

remapSourcesJar {
  archiveBaseName.set(project.property("archive_base_name").toString())
  archiveVersion.set(fullVersion)
  archiveClassifier.set("sources")
}
